import groovy.json.*

apply plugin: "java"

sourceCompatibility = 16

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

ext {
    def json = new JsonSlurper()
    def modFile = file("${sourceSets.main.resources.srcDirs.last()}/plugin.json")
    mod = json.parseText(modFile.text)
}

ext {
    mindustryVersion = "v145"
    lombokVersion = "1.18.24"
    discordWebhookVersion = "v1.6"
    jacksonYamlVersion = "2.13.0"
    jdaVersion = "5.0.0-alpha.18"
    okhttpVersion = "4.9.3"
    mysqlVersion = "8.0.30"
    jooqVersion = "3.17.0"
    databaseVersion = "ee150bbb7e"
}

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"

    compileOnly ("com.github.Anuken.MindustryJitpack:core:$mindustryVersion") {
        exclude module: "flabel"
        exclude module: "freetype"
        exclude module: "g3d"
        exclude module: "fx"
        exclude module: "arcnet"
    }
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"

    implementation ("net.dv8tion:JDA:$jdaVersion") { exclude module: "opus-java" }
    compileOnly "com.squareup.okhttp3:okhttp:$okhttpVersion"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonYamlVersion"

    compileOnly "org.jooq:jooq:$jooqVersion"
    compileOnly "mysql:mysql-connector-java:$mysqlVersion"
    implementation "com.github.thedimasNetwork:DatabaseWrapper:$databaseVersion"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveFileName = "${project.archivesBaseName}.jar"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from("assets/$mod.name/") {
        include "**"
    }
}

processResources {
    def build = (project.hasProperty("buildVersion") ? project.property("buildVersion") : "snapshot") + " | ${getGitHash()}"

    println("Compiling with build: '$build'")

    filesMatching("**/plugin.json") {
        from it.file
        filter { line -> line.replace("%buildVersion%", build) }
    }
}
