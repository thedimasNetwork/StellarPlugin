import groovy.json.*

apply plugin: 'java'

sourceCompatibility = 15

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

ext {
    def json = new JsonSlurper()
    def modFile = file("${sourceSets.main.resources.srcDirs.last()}/plugin.json")
    mod = json.parseText(modFile.text)
}

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.projectlombok:lombok:1.18.24'

    compileOnly "com.github.Anuken.Mindustry:core:v138"
    compileOnly "com.github.Anuken.Arc:arc-core:v138"

    implementation "com.github.thedimasNetwork:DiscordWebhook:v1.6"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0"
    runtimeOnly "mysql:mysql-connector-java:8.0.30"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveFileName = "${project.archivesBaseName}.jar"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from("assets/$mod.name/") {
        include "**"
    }
}

processResources {
    def build = project.hasProperty("buildVersion") ? project.property("buildVersion") : "snapshot"
    println("Compiling with build: '$build'")

    filesMatching("**/plugin.json") {
        from it.file
        filter { line -> line.replace("%buildVersion%", build as String) }
    }
}
