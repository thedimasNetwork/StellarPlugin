import groovy.json.*

apply plugin: 'java'

sourceCompatibility = 15

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

ext {
    def json = new JsonSlurper()
    def modFile = file("${sourceSets.main.resources.srcDirs.last()}/plugin.json")
    mod = json.parseText(modFile.text)
}

ext {
    mindustryVersion = "v138"
    lombokVersion = "1.18.24"
    discordWebhookVersion = "v1.6"
    jacksonYamlVersion = "2.13.0"
    jdaVersion = "5.0.0-alpha.18"
    mysqlVersion = "8.0.30"
}

repositories {
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"

    compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
    compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"

    implementation "com.github.thedimasNetwork:DiscordWebhook:$discordWebhookVersion"
    implementation ("net.dv8tion:JDA:$jdaVersion") { exclude module: "opus-java" }
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonYamlVersion"

    runtimeOnly "mysql:mysql-connector-java:$mysqlVersion"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveFileName = "${project.archivesBaseName}.jar"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from("assets/$mod.name/") {
        include "**"
    }
}

processResources {
    def build = project.hasProperty("buildVersion") ? project.property("buildVersion") : "snapshot"
    println("Compiling with build: '$build'")

    filesMatching("**/plugin.json") {
        from it.file
        filter { line -> line.replace("%buildVersion%", build as String) }
    }
}
